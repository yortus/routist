// TODO: restore 'method'


Pattern
=   "/"   !.   { return []; }
/   RestOfPatternStartingWithGlobstar / RestOfPatternStartingWithSegments;


Method
=   "ANY"i   { return null; }
/   ("GET"i / "POST"i / "PUT"i / "DELETE"i)


RestOfPatternStartingWithGlobstar
=   head:Globstar   tail:RestOfPatternStartingWithSegments?   { return [].concat([[]], {'**':head}, tail ? tail : [[]]); }


RestOfPatternStartingWithSegments
=   head:Segment+   tail:RestOfPatternStartingWithGlobstar?   { return [].concat([head], tail ? tail.slice(1) : []); }


Globstar
=   "/**"   { return ''; }
/   "/{..."   id:IDENTIFIER   "}"   { return id; }


Segment
=   "/"   rest:(RestOfSegmentStartingWithStar / RestOfSegmentStartingWithAffix)   { return rest; }


RestOfSegmentStartingWithStar
=   head:Star   tail:RestOfSegmentStartingWithAffix?   { return [].concat('', {'*':head}, tail ? tail : ''); }


RestOfSegmentStartingWithAffix
=   head:Affix   tail:RestOfSegmentStartingWithStar?   { return [].concat(head, tail ? tail.slice(1) : []); }


Star
=   "*"   !"*"   { return ''; }
/   "{"   id:IDENTIFIER   "}"   { return id; }


Affix
=   LITERAL_CHAR+   { return text(); }


//================================================================================
//=                                  TERMINALS                                   =
//================================================================================
LITERAL_CHAR    = [a-z0-9._-]i
IDENTIFIER      = [a-z_$]i   [a-z0-9_$]i*   { return text(); }
_ 'whitespace'  = [ \t]+   ("#" .*)?   { return text(); }
